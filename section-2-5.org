#+title: 2.5 Exercises

* Exercise 2.77
Louis Reasoner tries to evaluate the expression (magnitude z) where z is the object shown in Figure 2.24.  To his surprise, instead of the answer 5 he gets an error message from apply-generic, saying there is no method for the operation magnitude on the types (complex).  He shows this interaction to Alyssa P. Hacker, who says "The problem is that the complex-number selectors were never defined for complex numbers, just for polar and rectangular numbers.  All you have to do to make this work is add the following to the complex package:"

#+begin_src scheme :eval never
(put 'real-part '(complex) real-part)
(put 'imag-part '(complex) imag-part)
(put 'magnitude '(complex) magnitude)
(put 'angle '(complex) angle)
#+end_src

Describe in detail why this works.  As an example, trace through all the procedures called in evaluating the expression (magnitude z) where z is the object shown in Figure 2.24.  In particular, how many times is apply-generic invoked?  What procedure is dispatched to in each case?

** Solution
The structure of z is '(complex rectangular 3 . 4), so the type-tag of z is 'complex. The definition of magnitude is:
#+begin_src scheme :eval never
(define (magnitude z)
  (apply-generic 'magnitude z))
#+end_src
Thus when magnitude is called for z it will apply magnitude again to the contents of z, that is '(rectangular 3 . 4). Now, magnitude will apply-generic again to an object which its type-tag is 'rectangular, and then magnitude defined in the rectangular package will be applied to 3 . 4.

* Exercise 2.78
The internal procedures in the scheme-number package are essentially nothing more than calls to the primitive procedures +, -, etc.  It was not possible to use the primitives of the language directly because our type-tag system requires that each data object have a type attached to it.  In fact, however, all Lisp implementations do have a type system, which they use internally.  Primitive predicates such as symbol? and number?  determine whether data objects have particular types.  Modify the definitions of type-tag, contents, and attach-tag from 2.4.2 so that our generic system takes advantage of Scheme's internal type system.  That is to say, the system should work as before except that ordinary numbers should be represented simply as Scheme numbers rather than as pairs whose car is the symbol scheme-number.

** Solution
#+begin_src scheme
(define (attach-tag type-tag contents)
  (if (eq? type-tag 'scheme-number)
      contents
      (cons type-tag contents)))

(define (type-tag datum)
  (cond ((number? datum) 'scheme-number)
        ((pair? datum)
         (car datum))
        (else
         (error "Bad tagged datum: TYPE-TAG"
                datum))))

(define (contents datum)
  (cond ((number? datum) datum)
        ((pair? datum)
         (cdr datum))
        (else
         (error "Bad tagged datum: CONTENTS"
                datum))))
#+end_src

* Exercise 2.79
Define a generic equality predicate equ? that tests the equality of two numbers, and install it in the generic arithmetic package.  This operation should work for ordinary numbers, rational numbers, and complex numbers.

** Solution
#+begin_src scheme
(define (install-equality-package)
  (put 'equ?
       '(scheme-number scheme-number)
       (lambda (x y)
         (= x y)))
  (put 'equ?
       '(rational rational)
       (lambda (x y)
         (and (= (numerator x) (numerator y))
              (= (denominator x) (denominator y)))))
  (put 'equ?
       '(complex complex)
       (lambda (x y)
         (and (= (real-part x) (real-part y))
              (= (imag-part x) (imag-part y)))))
  'done)

(define (equ? x y)
  (apply-generic 'equ? x y))
#+end_src

* Exercise 2.80
Define a generic predicate =zero? that tests if its argument is zero, and install it in the generic arithmetic package.  This operation should work for ordinary numbers, rational numbers, and complex numbers.

** Solution
#+begin_src scheme
(define (install-zero-predicate-package)
  (put '=zero?
       '(scheme-number)
       (lambda (x)
         (= 0 x)))
  (put '=zero?
       '(rational)
       (lambda (x)
         (= 0 (numerator x))))
  (put '=zero?
       '(complex)
       (lambda (x)
         (and (= 0 (real-part x))
              (= 0 (imag-part x)))))
  'done)

(define (=zero? x)
  (apply-generic '=zero? x))
#+end_src

* Exercise 2.81
Louis Reasoner has noticed that apply-generic may try to coerce the arguments to each other’s type even if they already have the same type.  Therefore, he reasons, we need to put procedures in the coercion table to coerce arguments of each type to their own type.  For example, in addition to the scheme-number->complex coercion shown above, he would do:

#+begin_src scheme :eval never
(define (scheme-number->scheme-number n) n)
(define (complex->complex z) z)

(put-coercion 'scheme-number 'scheme-number
              scheme-number->scheme-number)

(put-coercion 'complex 'complex
              complex->complex)
#+end_src

1. With Louis’s coercion procedures installed, what happens if apply-generic is called with two arguments of type scheme-number or two arguments of type complex for an operation that is not found in the table for those types?  For example, assume that we’ve defined a generic exponentiation operation:

#+begin_src scheme :eval never
(define (exp x y)
  (apply-generic 'exp x y))
#+end_src

and have put a procedure for exponentiation in the Scheme-number package but not in any other package:

#+begin_src scheme :eval never
;; following added to Scheme-number package
(put 'exp
     '(scheme-number scheme-number)
     (lambda (x y)
       (tag (expt x y))))
       ; using primitive expt
#+end_src

What happens if we call exp with two complex numbers as arguments?

2. Is Louis correct that something had to be done about coercion with arguments of the same type, or does apply-generic work correctly as is?

3. Modify apply-generic so that it doesn’t try coercion if the two arguments have the same type.

** Solution
A.1: It will get into an infinite recursion because it will always find a coercion but never an operation.

A.2: No. It works just as intended.

3.A:
#+begin_src scheme
(define (apply-generic op . args)
  (define (type-error)
    (error "No method for these types"
           (list op (map type-tag args))))
  (let ((type-tags (map type-tag args)))
    (let ((proc (get op type-tags)))
      (if proc
          (apply proc (map contents args))
          (if (= (length args) 2)
              (let ((type1 (car type-tags))
                    (type2 (cadr type-tags))
                    (a1 (car args))
                    (a2 (cadr args)))
                (if (not (eq? type1 type2))
                    (let ((t1->t2 (get-coercion type1 type2))
                          (t2->t1 (get-coercion type2 type1)))
                      (cond (t1->t2
                             (apply-generic op (t1->t2 a1) a2))
                            (t2->t1
                             (apply-generic op a1 (t2->t1 a2)))
                            (else
                             (type-error))))
                    (type-error)))
              (type-error))))))
#+end_src

* Exercise 2.82
Show how to generalize apply-generic to handle coercion in the general case of multiple arguments.  One strategy is to attempt to coerce all the arguments to the type of the first argument, then to the type of the second argument, and so on. Give an example of a situation where this strategy (and likewise the two-argument version given above) is not sufficiently general.  (Hint: Consider the case where there are some suitable mixed-type operations present in the table that will not be tried.)

** Solution
#+begin_src scheme
(define (coerce target-type args)
  (map (lambda (arg)
         (let ((arg-type (type-tag arg)))
           (if (eq? target-type arg-type)
               arg
               (let ((coercion (get-coercion arg-type target-type)))
                 (if coercion
                     (coercion arg)
                     arg)))))
       args))

;; prevents duplicate coercions
(define (make-type-set type-tags)
  (define (adjoin set type)
    (cond ((null? set)
           (cons type '()))
          ((eq? type (car set))
           set)
        (else
         (cons (car set) (adjoin type (cdr set))))))
  (fold-left adjoin
             '()
             type-tags))

(define (apply-generic op . args)
  (define type-tags (map type-tag args))
  (define (helper type-set actual-args)
    (let ((proc (get op (map type-tag actual-args))))
      (if proc
          (apply proc (map contents actual-args))
          (if (null? type-set)
              (error "No method for these types: "
                     (list op type-tags))
              (helper (cdr type-set)
                      (coerce (car type-set) args))))))
  (helper (make-type-set type-tags) args))

#+end_src

* Exercise 2.83
Suppose you are designing a generic arithmetic system for dealing with the tower of types shown in Figure 2.25: integer, rational, real, complex.  For each type (except complex), design a procedure that raises objects of that type one level in the tower.  Show how to install a generic raise operation that will work for each type (except complex).

** Solution
#+begin_src scheme
(define (install-raise-package)
  (define (integer->rational num)
    (make-rational num 1))
  (define (rational->real num)
    (make-real (/ (car num) (cdr num))))
  (define (real->complex num)
    (make-complex-from-real-imag num 0))
  ;;
  (put 'raise '(integer) integer->rational)
  (put 'raise '(rational) rational->real)
  (put 'raise '(real) real->complex))

(define (raise x)
  (apply-generic 'raise x))
#+end_src

* Exercise 2.84
Using the raise operation of Exercise 2.83, modify the apply-generic procedure so that it coerces its arguments to have the same type by the method of successive raising, as discussed in this section.  You will need to devise a way to test which of two types is higher in the tower.  Do this in a manner that is "compatible" with the rest of the system and will not lead to problems in adding new levels to the tower.

** Solution
#+begin_src scheme
(define (sublist head ls)
  (if (or (null? ls)
          (eq? head (car ls)))
      ls
      (sublist head (cdr ls))))

(define (maximum greater? ls)
  (define (iter result ls)
    (if (null? ls)
        result
        (let ((head (car ls))
              (rest (cdr ls)))
          (if (greater? head result)
              (iter head rest)
              (iter result rest)))))
  (if (null? ls)
      '()
      (iter (car ls) (cdr ls))))

(define (higher-type? t1 t2)
  (let ((t2-tower (sublist t2 '(integer rational real complex))))
    (and (pair? t2-tower)
         (pair? (sublist t1 (cdr t2-tower))))))

(define (raise-all-to target args)
  (define (raise-while-target-is-higher arg)
    (if (higher-type? target (type-tag arg))
        (raise-while-target-is-higher (raise arg))
        arg))
  (map raise-while-target-is-higher args))

(define (level-up args)
  (raise-all-to
   (maximum higher-type? (map type-tag args))
   args))

(define (apply-generic op . args)
  (define (apply-generic-helper op args)
    (let ((type-tags (map type-tag args)))
      (let ((proc (get op type-tags)))
        (if proc
            (apply proc (map contents args))
            (let ((raised (level-up args)))
              (if (equal? raised args)
                  (error "No method for these types: "
                         (list op type-tags))
                  (apply-generic-helper op raised)))))))
  (apply-generic-helper op args))
#+end_src
